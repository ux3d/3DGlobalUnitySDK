#pragma kernel FXAA

#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Common.hlsl"
#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Color.hlsl"
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/ShaderLibrary/ShaderVariables.hlsl"
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/Material/Builtin/BuiltinData.hlsl"
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/PostProcessing/Shaders/PostProcessDefines.hlsl"

RWTexture2D<float4> _OutputTexture;
Texture2D _colorMosaic;
SamplerState sampler_colorMosaic;

#define FXAA_SPAN_MAX       (4.0)
#define FXAA_REDUCE_MUL     (1.0 / 4.0)
#define FXAA_REDUCE_MIN     (1.0 / 32.0)
// original:
// #define FXAA_SPAN_MAX       (8.0)
// #define FXAA_REDUCE_MUL     (1.0 / 8.0)
// #define FXAA_REDUCE_MIN     (1.0 / 128.0)

float LuminanceGamma(float3 c) {
    float3 w = float3(0.299, 0.587, 0.114);
    return sqrt(dot(c, w));
}

float3 Fetch(TEXTURE2D_PARAM(_InputTexture, _InputTextureSampler), float2 coords, float2 offset)
{
    float2 uv = saturate(coords + offset) * _RTHandlePostProcessScale.xy;
    return SAMPLE_TEXTURE2D_LOD(_InputTexture, _InputTextureSampler, uv, 0.0).xyz;
}

float4 Load(TEXTURE2D(_InputTexture), int2 icoords, int idx, int idy)
{
    return LOAD_TEXTURE2D(_InputTexture, min(icoords + int2(idx, idy), _PostProcessScreenSize.xy - 1.0));
}

float FetchAlpha(TEXTURE2D_PARAM(_InputTexture, _InputTextureSampler), float2 coords, float2 offset)
{
    float2 uv = saturate(coords + offset) * _RTHandleScale.xy;
    return SAMPLE_TEXTURE2D_LOD(_InputTexture, _InputTextureSampler, uv, 0.0).w;
}

void RunFXAA(TEXTURE2D_PARAM(_InputTexture, _InputTextureSampler), inout float4 outColor, uint2 positionSS, float2 positionNDC)
{
    // Edge detection
    float4 rgbNW = Load(_InputTexture, positionSS, -1, -1);
    float4 rgbNE = Load(_InputTexture, positionSS, 1, -1);
    float4 rgbSW = Load(_InputTexture, positionSS, -1, 1);
    float4 rgbSE = Load(_InputTexture, positionSS, 1, 1);

    rgbNW.xyz = saturate(rgbNW.xyz);
    rgbNE.xyz = saturate(rgbNE.xyz);
    rgbSW.xyz = saturate(rgbSW.xyz);
    rgbSE.xyz = saturate(rgbSE.xyz);
    outColor.xyz = saturate(outColor.xyz);
    float lumaNW = LuminanceGamma(rgbNW.xyz);
    float lumaNE = LuminanceGamma(rgbNE.xyz);
    float lumaSW = LuminanceGamma(rgbSW.xyz);
    float lumaSE = LuminanceGamma(rgbSE.xyz);
    float lumaM = LuminanceGamma(outColor.xyz);

    float2 dir;
    dir.x = -((lumaNW + lumaNE) - (lumaSW + lumaSE));
    dir.y = ((lumaNW + lumaSW) - (lumaNE + lumaSE));

    float lumaSum = lumaNW + lumaNE + lumaSW + lumaSE;
    float dirReduce = max(lumaSum * (0.25 * FXAA_REDUCE_MUL), FXAA_REDUCE_MIN);
    float rcpDirMin = rcp(min(abs(dir.x), abs(dir.y)) + dirReduce);

    dir = min((FXAA_SPAN_MAX).xx, max((-FXAA_SPAN_MAX).xx, dir * rcpDirMin)) * _ScreenSize.zw;

    // Blur
    float3 rgb03 = Fetch(TEXTURE2D_ARGS(_InputTexture, _InputTextureSampler), positionNDC, dir * (0.0 / 3.0 - 0.5));
    float3 rgb13 = Fetch(TEXTURE2D_ARGS(_InputTexture, _InputTextureSampler), positionNDC, dir * (1.0 / 3.0 - 0.5));
    float3 rgb23 = Fetch(TEXTURE2D_ARGS(_InputTexture, _InputTextureSampler), positionNDC, dir * (2.0 / 3.0 - 0.5));
    float3 rgb33 = Fetch(TEXTURE2D_ARGS(_InputTexture, _InputTextureSampler), positionNDC, dir * (3.0 / 3.0 - 0.5));

    rgb03 = saturate(rgb03);
    rgb13 = saturate(rgb13);
    rgb23 = saturate(rgb23);
    rgb33 = saturate(rgb33);

    float3 rgbA = 0.5 * (rgb13 + rgb23);
    float3 rgbB = rgbA * 0.5 + 0.25 * (rgb03 + rgb33);

    float lumaB = LuminanceGamma(rgbB);

    float lumaMin = Min3(lumaM, lumaNW, Min3(lumaNE, lumaSW, lumaSE));
    float lumaMax = Max3(lumaM, lumaNW, Max3(lumaNE, lumaSW, lumaSE));

    float3 rgb = ((lumaB < lumaMin) || (lumaB > lumaMax)) ? rgbA : rgbB;

    outColor.xyz = rgb;
}


[numthreads(1, 1, 1)]
void FXAA(uint3 dispatchThreadId : SV_DispatchThreadID)
{
    UNITY_XR_ASSIGN_VIEW_INDEX(dispatchThreadId.z);
    uint2 positionSS = dispatchThreadId.xy;
    float2 positionNDC = positionSS * _PostProcessScreenSize.zw + (0.5 * _PostProcessScreenSize.zw);

    float4 outColor = _colorMosaic.Load(float4(positionSS, 0, 0));
    float4 beforeFXAA = outColor;

    RunFXAA(_colorMosaic, s_linear_clamp_sampler, outColor, positionSS, positionNDC);

    _OutputTexture[positionSS] = outColor;
}
